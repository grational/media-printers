/*
 * This build file was auto generated by 'gigawatt'
 * @author gsus
 * @date 17-11-2016 12.50
 */

// [plugins] this block must be the first
plugins {
	// support to api keyword
	id 'java-library'
	// support for groovy
	id 'groovy'
	// autogenerate eclipse project files
	id 'eclipse'
	// create and html index linking every report generated
	id 'build-dashboard'
	// view of the project (jars used, navigable source tree)
	id 'project-report'
	// groovy code linter
	id 'codenarc'
	// test code coverage plugin
	id 'net.saliman.cobertura' version '2.6.0'
	// awesome git tag / gradle version integration
	id 'pl.allegro.tech.build.axion-release' version '1.10.0'
	// to upload on a nexus server
	id 'maven'
}

// [general] configuration
project.ext {
	groovyVersion   = '2.5.6'
	gradleVersion   = '5.2.1'
	spockVersion    = '1.3-groovy-2.5'
	codenarcVersion = '1.3'
	slf4jVersion    = '1.7.26'
	wrapper.gradleVersion "$gradleVersion"
}

project.with {
	//project.name is set in the settings.gradle with rootProject.name
	group                 'it.italiaonline.rnd'
	version               scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)
	description           'Provide and object the functionality to print itself on a media (currently csv,json)'
}

|// [crosscompile] source and target java version
allprojects {
	sourceCompatibility = 8
	targetCompatibility = sourceCompatibility
}

repositories {
	jcenter()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
	// Groovy language support
	api (
		"org.codehaus.groovy:groovy:$groovyVersion"
	)

	implementation (
		"org.codehaus.groovy:groovy-json:$groovyVersion"
	)

	// We use the awesome Spock testing and specification framework
	testCompile "org.spockframework:spock-core:$spockVersion"
}

// codenarc groovy source analyses
codenarc{
	toolVersion = "$codenarcVersion"

	codenarcMain {
		configFile = rootProject.file('config/codenarc/ruleset.groovy')
		exclude "**/package-info.*"
		ignoreFailures true       // does not block builds
		maxPriority1Violations 0
		maxPriority2Violations 10
		maxPriority3Violations 20
	}
	codenarcTest {
		configFile = rootProject.file('config/codenarc/ruleset-test.groovy')
		ignoreFailures true
		maxPriority1Violations 0
		maxPriority2Violations 10
		maxPriority3Violations 20
	}
}

// [tasks] configuration //
jar {
	// execute check task when create the jar
	dependsOn 'check'
}

check {
	// clean before check
	dependsOn 'clean'
	// create the code coverage report
	dependsOn 'cobertura'
	// set the correct order for the dep tasks
	tasks.findByName('cobertura').mustRunAfter 'clean'
}

// [tasks] definition
task listJars {
	description = 'print out the jars used by the project'
	doLast {
		configurations.compile.each { File file -> println file.name }
	}
}

// [maven] upload to http nexus configuration
configurations {
	deployerJars
}
dependencies {
	// needed to deploy on nexus via http
	deployerJars "org.apache.maven.wagon:wagon-http:2.+"
}
uploadArchives {
	repositories.mavenDeployer {
		configuration = configurations.deployerJars
		repository(url: "${nexusUrl}/content/repositories/releases") {
			authentication(userName: nexusUsername, password: nexusPassword)
		}
		snapshotRepository(url: "${nexusUrl}/content/repositories/snapshots") {
			authentication(userName: nexusUsername, password: nexusPassword)
		}
	}
}

// vim: ft=groovy:fdm=indent
